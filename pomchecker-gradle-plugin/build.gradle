/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2024 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'groovy'
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.plugin'
}

config {
    info {
        name          = rootProject.name
        description   = 'Pomchecker Gradle Plugin'
        inceptionYear = '2020'
        tags          = ['pom', 'validation']

        links {
            website      = 'https://github.com/kordamp/pomchecker'
            issueTracker = 'https://github.com/kordamp/pomchecker/issues'
            scm          = 'https://github.com/kordamp/pomchecker.git'
        }

        scm {
            url                 = 'https://github.com/kordamp/pomchecker'
            connection          = 'scm:git:https://github.com/kordamp/pomchecker.git'
            developerConnection = 'scm:git:git@github.com:kordamp/pomchecker.git'
        }

        specification { enabled = false }
    }

    docs {
        groovydoc {
            replaceJavadoc = true
        }
    }

    plugins {
        plugin {
            name                = 'pomchecker'
            id                  = 'org.kordamp.gradle.pomchecker'
            implementationClass = 'org.kordamp.gradle.plugin.checker.PomCheckerPlugin'
        }
    }
}

repositories {
    flatDir { dir 'target/dependencies' }
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()

    api "org.kordamp.maven:pomchecker-core:$version"
    api "org.apache.maven:maven-model:$mavenVersion"
    api "eu.maveniverse.maven.mima:context:$mimaVersion"
    api "eu.maveniverse.maven.mima.extensions:mmr:$mimaVersion"
    api "eu.maveniverse.maven.mima.runtime:standalone-sisu:$mimaVersion"
    api "org.kordamp.gradle:base-gradle-plugin:$kordampPluginVersion"
    api "org.eclipse.sisu:org.eclipse.sisu.inject:$sisuVersion"
//    api "org.eclipse.sisu:org.eclipse.sisu.plexus:$sisuVersion"
}

processResources {
    inputs.property('version', project.version)
    filesMatching(['**/*.properties']) {
        expand(
            'version': project.version,
            'id': 'pomchecker',
            'name': 'PomChecker Gradle plugin'
        )
    }
}

println "Version = ${project.version}"